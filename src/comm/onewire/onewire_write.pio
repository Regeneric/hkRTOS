; -----------------------------------------------------------------------------
; onewire_write.pio  â€” 1-Wire protocol implementation - 1 MHz clock
; -----------------------------------------------------------------------------

; 1-Wire Write Time Slot Sequence (for a single bit)
;
; Phase             Action                          Purpose & Timing
; ----------------- ------------------------------- ----------------------------------------------------
; Initiate Slot     Master pulls line LOW           Signals the start of a write time slot to all slaves.
;
; Transmit '1'      Master holds LOW for a SHORT    (~5-15us) The slave device samples the line and
;                   time, then releases (HIGH).     registers a '1' bit.
;
; Transmit '0'      Master holds LOW for a LONG     (~60us) The slave device samples the line and
;                   time, then releases (HIGH).     registers a '0' bit.
;
; Slot Recovery     The line stays HIGH after the   Provides the required recovery time before the
;                   pulse for the remainder of      next time slot can begin.
;                   the slot.

.program onewire_write
.wrap_target
    ; pull block              ;   Pull command from FIFO to osr
    set y, 7                ;   Byte loop iterator

bit_loop:
    out x, 1                ;   Shift one bit from osr to x
    jmp !x, send_zero       ;   We've got a logical '1'; otherwise we've got a logical '0'   

send_one:
    set pindirs, 1 [4]      ;   DATA as output; drive DATA LOW (~5us)
    set pindirs, 0          ;   DATA as input; drive DATA HIGH
    jmp bit_loop_end        ;   Read another bit

send_zero:
    set pindirs, 1 [31]     ;   DATA as output; drive DATA LOW (~60us)
    nop [27]                ;   Wait till the write time slot for 0 is met
    set pindirs, 0          ;   Data as input; drive DATA HIGH

bit_loop_end:
    jmp y--, bit_loop       ;   Read another bit
.wrap
;; 10 instructions in total

% c-sdk {
static inline void onewire_write_program_init(PIO pio, uint sm, uint offset, uint pin) {    
    pio_sm_config cfg = onewire_write_program_get_default_config(offset);
    sm_config_set_clkdiv(&cfg, ((float)clock_get_hz(clk_sys) / 1000000.0f));
    sm_config_set_in_pins(&cfg, pin);
    sm_config_set_set_pins(&cfg, pin, 1);
    sm_config_set_out_pins(&cfg, pin, 1);
    
    sm_config_set_out_shift(&cfg, false, true, 8);

    pio_sm_set_consecutive_pindirs(pio, sm, pin, 1, false);
    pio_gpio_init(pio, pin);
    
    pio_sm_init(pio, sm, offset, &cfg);
}
%}